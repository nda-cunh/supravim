vim9script

#**** SUPRAVIM ****"
set nocompatible
filetype off

import autoload 'supraload.vim' as SupraLoad 
SupraLoad.Infect()

filetype plugin indent on

import autoload 'SupraMake.vim' as Make
import autoload 'SupraNotification.vim' as Notify
import autoload 'SupraPopup.vim' as Popup 
import autoload 'SupraUtils.vim' as Utils
import autoload 'supravim/Welcome.vim' as WelcomeModule
import autoload 'supravim/SupravimServer.vim' as SupraVimServer
import autoload 'supravim/SupraOptions.vim' as Options

g:sp_swap = false	# create swapfile (.swp)
g:sp_wrap = true	# do not wrap the text if the line is too long
g:sp_autosave = true	# Auto save files
g:sp_nobackground = false	# remove background color and use your terminal background
g:sp_autopairs_enabled = true	# enable auto pairs
g:sp_autopairs_flymode = false	# Jump out of closing pairs when typing the closing character
g:sp_supratree_open = true # display folder tree at left
g:sp_supratree_winsize = 26	# size of the folder tree window by default
g:sp_supratree_show_hidden = false	# show hidden files in the folder tree
g:sp_supratree_sortascending = true	# sort the folder tree ascending
g:sp_supratree_theme_darken_percent = 18 # darken percent of the folder tree	
g:sp_supratree_theme_forcecolor = '' # force color for supratree, like #adedb8, empty for default
g:sp_fuzzbox_enable_icons = true # enable icons in fuzzbox
g:sp_fuzzbox_dropdown = false # prompt at top rather than bottom
g:sp_fuzzbox_counter = true # Show count of matches 
g:sp_fuzzbox_scrollbar = false # Show a scrollbar in the menu window when the results do not fit 
g:sp_fuzzbox_prompt_prefix = '➜ ' # Set the prefix for the input in the prompt window 
g:sp_fuzzbox_wrap_menu = false # wrap menu lines in fuzzbox
g:sp_fuzzbox_wrap_preview = true # wrap preview lines in fuzzbox
g:sp_roundvisual_quotes = true	# Auto Closing for quotes '"' and ''' and '`'
g:sp_roundvisual_brackets = true	# Auto Closing for brackets'(' ')' and '[' ']'
g:sp_roundvisual_braces = false	# Auto Closing for braces '{' '}'
g:sp_roundvisual_angle = false	# Auto Closing for angle brackets '<' '>'
g:sp_symbol_signs = "┃"	# The symbol used for the signs before it is this: ✖
g:sp_mapleader = "\<space>"	# The leader key used in the mappings
g:sp_theme_style_function = 1	# 0 = none, 1 = bold, 2 = italic, 3 = underline
g:sp_theme_style_type = 0	# 0 = none, 1 = bold, 2 = italic, 3 = underline
g:sp_theme_style_variable = 0	# 0 = none, 1 = bold, 2 = italic, 3 = underline
g:sp_theme_style_keyword = 1	# 0 = none, 1 = bold, 2 = italic, 3 = underline
g:sp_theme_style_comment = 0	# 0 = none, 1 = bold, 2 = italic, 3 = underline
g:sp_theme = "onedark"	# A cute theme color

g:mapleader = g:sp_mapleader

if g:sp_autosave == true
	Utils.AutoSaveActivate(true)
endif

# Protect against invalid symbols
if true
	g:sp_symbol_signs = substitute(g:sp_symbol_signs, '\s+', '', 'g')
	const len = strcharlen(g:sp_symbol_signs)
	if len > 2
		call Notify.Notification(["Invalid symbol", "The symbol used for the signs must be a single character"], {type: 'error'})
		g:sp_symbol_signs = '✖'
	elseif len == 0
		call Notify.Notification(["Invalid symbol", "The symbol used for the signs must not be empty"], {type: 'error'})
		g:sp_symbol_signs = '✖'
	endif
endif

#--------------- Colors Theme ---------------"
syntax on

if (has("termguicolors"))
	set termguicolors
endif

set t_Co=256
exec 'colorscheme ' .. g:sp_theme
set background=dark

if g:sp_nobackground == true
	hi Normal guibg=NONE ctermbg=NONE
	hi! link SignColumn Normal
	hi! link EndOfBuffer Normal
	# hi! link CursorLine Normal 
endif

#------------- Cursor Parts ------------"
&t_SI = "\<Esc>[5 q"
&t_SR = "\<Esc>[3 q"
&t_EI = "\<Esc>[2 q"
autocmd VimLeave * !echo -ne "\e[0 q"

#-------------- Save Undo  ------------"
set undodir=~/.cache/vim/undo
set undofile
set viminfo+=!

# -------------  DBG integration  --------------"
packadd! termdebug
g:termdebug_wide = 1

#--------------- Shorcut --------------"
nnoremap <c-n>			<esc>:tabnew 
nnoremap <C-Right>		<scriptcmd>tabnext<CR>
nnoremap <C-Left>		<scriptcmd>tabprevious<CR>
nnoremap <C-Up>		<scriptCmd>call Ctags()<CR>
nnoremap <C-Down>	<scriptcmd>pop<cr>
inoremap <C-Up>		<scriptcmd>call Ctags()<CR>
inoremap <C-Down>	<scriptcmd>pop<cr>
inoremap <c-q>		<cmd>q!<CR>
noremap <c-q>		<cmd>q!<CR>
inoremap <c-s>		<cmd>w!<CR><esc>
noremap <c-s>		<cmd>w!<CR><esc>
nnoremap <C-F5> 	<cmd>Termdebug -n <CR>
nnoremap <F2> 		<ScriptCmd>call Utils.RenameSymbol()<CR>
inoremap <F2> 		<ScriptCmd>call Utils.RenameSymbol()<CR>
nnoremap <leader>v		:vs<cr>
nnoremap <leader>s		:split<cr>
nnoremap <S-Right>	<C-w><Right>
nnoremap <S-Left>	<C-w><Left>
nnoremap <S-Up>		<C-w><Up>
nnoremap <S-Down>	<C-w><Down>
nnoremap <S-Tab>	<<
nnoremap <Tab>		>>
vnoremap <S-Tab>	<<<esc>gv
vnoremap <Tab>		>><esc>gv
inoremap <c-a>		<esc>gg<s-v>G
nnoremap <silent><c-@>		<cmd>LspHover<cr>
silent! unmap <c-t>
nnoremap <C-t>		<scriptcmd>term ++rows=15<CR>
# Terminal shortcuts
map <leader>tt	<scriptcmd>call SupraTerm()<cr>
map <leader>tf	<scriptcmd>call SupraTerm()<cr>
map <leader>th	:horizontal term<CR>
map <leader>tv	:vertical term<CR>
# Lsp shortcuts 
nnoremap <silent> gd		<cmd>LspDefinition<cr>
nnoremap <silent> gr		<cmd>LspReferences<cr>
nnoremap <silent> gi		<cmd>LspImplementation<cr>
nnoremap <silent> gD		<cmd>LspTypeDefinition<cr>
nnoremap <silent> gt		<cmd>LspTypeDefinition<cr>
nnoremap <silent> g0		<cmd>LspDocumentSymbol<cr>
nnoremap <silent> gW		<cmd>LspWorkspaceSymbol<cr>
nnoremap <silent> ga		<cmd>LspCodeAction<cr>
nnoremap <silent> K			<cmd>LspHover<cr>

# Other
inoremap <c-z>		<esc>ui
inoremap <c-y>		<esc><c-r>i

# Move lines
vnoremap <silent> <A-Down>	:<scriptcmd>Utils.MoveLineDown("v")<CR><cr>
vnoremap <silent> <A-Up>	:<scriptcmd>Utils.MoveLineUp("v")<CR><cr>
nnoremap <silent> <A-Down>	<scriptcmd>Utils.MoveLineDown("n")<CR>
nnoremap <silent> <A-Up>	<scriptcmd>Utils.MoveLineUp("n")<CR>

# Fuzzy Finder
nnoremap <silent> <leader>g			<scriptcmd>FuzzyGrep<CR>
nnoremap <silent> <leader>m			<scriptcmd>FuzzyMarks<CR>
nnoremap <silent> <leader>f			<scriptcmd>FuzzyFiles<CR>
nnoremap <silent> <leader>d			<scriptcmd>FuzzyLspDiag<CR>
nnoremap <silent> <leader>b			<scriptcmd>FuzzyBuffers<CR>
nnoremap <silent> <leader>h			<scriptcmd>FuzzyHighlights<CR>
nnoremap <silent> <leader>c			<scriptcmd>FuzzyClip<cr>
nnoremap <silent> <leader>p			<scriptcmd>FuzzyPrevious<cr>
nnoremap <silent> <leader><f5>		<scriptcmd>FuzzyMake<cr>
# nnoremap <silent> <leader>t			<scriptcmd>FuzzySupraTags<cr>
nnoremap <silent> <leader><leader>	<scriptcmd>FuzzyBuffers<CR>

# SupraVim Settings
inoremap <F12>	<scriptcmd>call SettingsSupravim()<cr>
noremap <F12>	<scriptcmd>call SettingsSupravim()<cr>
# Mouse
nnoremap <RightMouse> <esc><LeftMouse><esc><RightMouse>
inoremap <RightMouse> <esc><LeftMouse><esc><RightMouse>
nnoremap <c-leftmouse>	<leftmouse><scriptcmd>call Ctags()<cr>

#---------------      Command        ---------------"
cmap <c-q>	q!<cr>
cmap <c-s>	w!<cr>
cmap <c-v>	<c-r>0<del>

#---------------      Terminal        ---------------"
tnoremap <C-q> exit<CR>
tnoremap <F3> clear -x ; norminette<CR>
tnoremap <F5> supramake run<CR>
tnoremap <F6> supramake run2<CR>
tnoremap <F7> supramake run3<CR>
tnoremap <esc>	<c-\><c-n>
# Window navigation
tnoremap <S-Right>		<C-W>N<C-w><Right>
tnoremap <S-Left>		<C-W>N<C-w><Left>
tnoremap <S-Up>			<C-W>N<C-w><Up>
tnoremap <S-Down>		<C-W>N<C-w><Down>

#--------------- 	Settings      ---------------"
g:lsp_log_verbose = 0
set mouse=a
if g:sp_swap == false
	set noswapfile
endif
if g:sp_wrap == true
	set wrap
else
	set nowrap
endif
set cursorline
set shortmess+=I
set nu
set incsearch
if executable('suprapack') == 0
	autocmd VimEnter * call Notify.Notification(["Suprapack is not found", "Please source the $HOME/.suprapack_profile", "Or add ~/.local/bin to your path"], {type: 'warning'})
	$PATH = $PATH .. ':' .. expand('$HOME') .. '/.local/bin:'
endif
set noautochdir
silent! exec 'cd ' .. expand('%:p:h')
silent! var project_root = system('suprabear -p')
var suprabear_return = v:shell_error
silent! exec 'chdir ' .. project_root
set title
set wildmenu
set tabstop=4
set shiftwidth=4
set smartindent
set autoindent
set shiftround
set showmode
set backspace=indent,eol,start
set pumheight=50
set notimeout
set ttimeout
set timeoutlen=100
set encoding=utf-8
set splitbelow
set splitright
au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif
set fillchars=vert:│
auto BufEnter,VimEnter *.tpp set filetype=cpp

# ---------------      LSP            ---------------"
set updatetime=500
autocmd CursorHold * call Utils.AutoLspHold()

#--------------- Lsp Interface ---------------"
def g:AddLspServer(name: string, cmd: list<string>, filetypes: list<string>)
	silent! lsp#register_server({
		\ name: name,
		\ cmd: (_) => cmd,
		\ allowlist: filetypes,
	\ })
	silent! lsp#activate()
enddef

# --------------- Auto Pairs ---------------"
g:AutoPairsFlyMode = 0

#--------------- Supravim Options ---------------"
g:supravim_option_changed = 'null'
g:supravim_option_value = 'null'

def g:ChangeSupravimTheme(theme: string, typemode: string)
	Options.ChangeSupravimTheme(theme, typemode)
enddef

def g:ChangeSupravimOption(changed: string, value: string)
	Options.ChangeSupravimOption(changed, value)
enddef

def g:SettingsSupravim()
	job_start(["supravim-gui"], {
		out_cb: Options.GotOutput,
	})
enddef

# ---------------  SupraVim Server  ---------------"
g:supravim_server = job_start(["supravim-server", project_root], {
	out_cb: SupraVimServer.GotOutputSupravimServer,
	in_mode: 'raw',
	in_io: 'pipe'
})

def OpenNewFile()
	var filetype = &filetype
	if filetype == ''
		filetype = expand('%:e')
	endif
	ch_sendraw(g:supravim_server, "OpenFile: " .. filetype ..  "|" .. expand('%:p:t') .. "\n")
enddef

au BufEnter * OpenNewFile()

autocmd User SupravimChangeOption call Options.SimpleSupravimChangeOption()

# ---------------  GDB  ---------------"

imap <C-F5>		<ScriptCmd>Gdbs<CR>
map <C-F5>		<ScriptCmd>Gdbs<CR>

command -nargs=0 -bar Gdbs :call Gdbf()

def Gdbf()
	if &filetype != 'c' && &filetype != 'cpp' && &filetype != 'vala' && &filetype != 'hpp'
		echo "Tu veux debugger quoi là ?"
	else
		set splitbelow nosplitbelow
		set splitright nosplitright
		SupraTreeClose
		if !filereadable("Makefile")
			exec ":Termdebug -n ./a.out"
		else
			exec ":Termdebug -n"
		endif
	endif
	set splitbelow
	set splitright
enddef
# -------------- Ctags ----------------"

command -nargs=0 -bar Ctags :call Ctags()

set tags=$HOME/.cache/tags

def Ctags()
	try
	LspDefinition
	catch
		system("supratags --output=" .. expand("$HOME") .. "/.cache/tags")
		execute "tag " .. expand("<cword>")
	endtry
enddef

# ----------------- AIR LINE ------------------
g:airline#extensions#whitespace#enabled = 0
g:airline_section_z = airline#section#create(['Line:%l', 'hunks', ' Col:%c'])
g:airline_section_b = airline#section#create(['SupraVim'])
g:airline_left_sep = '◤'
g:airline_left_alt_sep = '╱'
g:airline_right_sep = '◥'
g:airline_right_alt_sep = '╲'
g:airline#extensions#tabline#enabled = 1
g:airline#extensions#tabline#show_buffers = 0
g:airline#extensions#tabline#tabs_label = 'Tabs'
g:airline#extensions#tabline#buffer_nr_show = 0
g:airline#extensions#tabline#tab_nr_type = 1
g:airline#extensions#tabline#left_sep = '◤'
g:airline#extensions#tabline#left_alt_sep = '╱'
g:airline#extensions#tabline#right_sep = '◥'
g:airline#extensions#tabline#right_alt_sep = '╱'

# ----------------- FuzzBox ------------------
g:fuzzbox_enable_mappings = 0
g:fuzzbox_files_exclude_file = ['*.o', '*.png', '*.jpg', '*.gif', 'compile_commands.json', '*.beam', '*.so', '*.exe', '*.dll', '*.dump', '*.core', '*.swn', '*.swp', '*.a', '*.out']
g:fuzzbox_files_exclude_dir = ['build', '*.cache', '.git', '.hg', '.svn', '.rebar', '.eunit']

# ----------------- POPUP ------------------
autocmd VimEnter * CreatePopit()

def PopUpUpdate(channel: channel, msg: string)
	Notify.Notification(["Update", msg, "run: `suprapack update`", "Click here to update"], {
		left_click: (_) => {
			!suprapack update
		},
		icon: '󰚰'
	})
enddef

def CreatePopit()
	job_start(["suprapack", "have_update", "supravim"], {
		out_cb: PopUpUpdate,
	})
enddef

# ---------------  Round It ---------------"
Options.AutoCloseActivate()
autocmd User SupraVimChangeOption call Options.AutoCloseActivate

# ---------------  Popup Menu  ---------------"
set mousemodel=popup
autocmd BufEnter * call WelcomeModule.Init_menu()

# ---------------  SetARGS  ---------------"
command -nargs=* -complete=file SetArgv :call SetArgv(<f-args>)
command -nargs=* -complete=file SetArgs :call SetArgv(<f-args>)

def SetArgv(...args: list<string>)
	var str = join(args, ' ')
	$ARGS = str
enddef

# ---------------  Welcome ---------------"
autocmd BufEnter * {
	if expand('%:e') == 'water'
		&titlestring = '[Supravim] Editing on SupraWater'
	else
		&titlestring = '[Supravim]  ' .. expand('%:t')
	endif
}

g:loaded_netrw = 1
g:loaded_netrwPlugin = 1

# Force directory creation on save
autocmd BufWritePre * {
if filereadable(expand('%')) == 0
	mkdir(expand('%:p:h'), 'p')
endif
}
# --------------- AutoPairs ---------------"
g:AutoPairsEnabled = g:sp_autopairs_enabled
g:AutoPairsFlyMode = g:sp_autopairs_flymode

def AutoPairsSupravimOption()
	if stridx(g:supravim_option_changed, 'autopairs_') != 0
		return
	endif
	if g:supravim_option_changed == 'autopairs_enabled'
		if g:supravim_option_value == 'true'
			call g:AutoPairsEnable()
		else
			call g:AutoPairsDisable()
		endif
	elseif g:supravim_option_changed == 'autopairs_flymode'
		if g:supravim_option_value == 'true'
			g:AutoPairsFlyMode = 1
		else
			g:AutoPairsFlyMode = 0
		endif
	endif
enddef

au User SupravimChangeOption call AutoPairsSupravimOption()

#--------------- SupraTree ---------------"
g:SupraTreeIgnoreTree = ['\.png$\', '\.jpg$', '\.o$']
g:SupraTreeWinSize = g:sp_supratree_winsize
g:SupraTreeShowHidden = g:sp_supratree_show_hidden
g:SupraTreeSortAscending = g:sp_supratree_sortascending
g:SupraTreeDarkenAmount = g:sp_supratree_theme_darken_percent
if g:sp_supratree_theme_forcecolor != ''
	g:SupraTreeForceColor = g:sp_supratree_theme_forcecolor
endif
g:SupraTreeOpenByDefault = g:sp_supratree_open
g:SupraTreeSymbolSigns = g:sp_symbol_signs
nnoremap <c-g> <cmd>SupraTreeToggle<cr> 

# --------------- FuzzBox ---------------"
g:fuzzbox_devicons = g:sp_fuzzbox_enable_icons
g:fuzzbox_dropdown = g:sp_fuzzbox_dropdown
g:fuzzbox_counter = g:sp_fuzzbox_counter
g:fuzzbox_scrollbar = g:sp_fuzzbox_scrollbar
g:fuzzbox_prompt_prefix = g:sp_fuzzbox_prompt_prefix
g:fuzzbox_menu_wrap = g:sp_fuzzbox_wrap_menu
g:fuzzbox_preview_wrap = g:sp_fuzzbox_wrap_preview

#--------------- SupraVim Color ----------"
def FixSupraVimColor()
	hi! link SignColumn Normal
	hi! link EndOfBuffer Normal
enddef

autocmd Colorscheme * call FixSupraVimColor()
FixSupraVimColor()

autocmd VimEnter * call WelcomeModule.PrintChangeLog()
autocmd User SupravimChangeOption call Options.ChangeColorStyle()
autocmd SourcePost * {
	call Options.ChangeColorStyle()
}

# for the bug with line with space
hi mkdLineBreak cterm=NONE

#--------------- Games ---------------"
autocmd VimEnter __SNAKE__ {
	:Snake
}

autocmd VimEnter __PACMAN__ {
	:Pacman
}

autocmd VimEnter * call WelcomeModule.Welcome()
