vim9script

#**** SUPRAVIM ****"
set nocompatible
filetype off

#--------------- Colors Theme ---------------"
syntax on

if (has("termguicolors"))
	set termguicolors
endif

set t_Co=256
set background=dark
# ---------------  Supraload  ---------------"

import autoload 'supraload.vim' as SupraLoad 
import autoload 'supraconfig.vim' as SupraConfig

{
	const lst_glob = glob($HOME .. "/.vim/bundle_supravim/*") .. "\n" .. glob(expand("$HOME") .. "/.vim/bundle_supravim/*/after")
	const lst = substitute(lst_glob, "\n", ",", "g")
	execute "set runtimepath+=" .. lst 
}
# Load plugins in bundles directories
SupraLoad.Infect()

filetype plugin indent on

import autoload 'SupraMake.vim' as Make
import autoload 'SupraNotification.vim' as Notify
import autoload 'SupraPopup.vim' as Popup 
import autoload 'SupraUtils.vim' as Utils
import autoload 'supravim/Welcome.vim' as WelcomeModule
import autoload 'supravim/SupraOptions.vim' as Options

if !has('patch-9.1.1100')
	Notify.Notification(["Unsupported Vim version !!!",
		"Some features may not work properly",
		"Please update to Vim 9.1.1100 or later"
	], {type: 'error'})
endif

SupraConfig.RegisterGroup('autopairs', 'Options related to the Auto Pairs plugin')
SupraConfig.RegisterGroup('fuzzbox', 'Options related to the Fuzzbox plugin')
SupraConfig.RegisterGroup('roundvisual', 'Options related to the RoundVisual plugin for auto closing')
SupraConfig.RegisterGroup('theme/style', 'Options related to the styling of the current theme')
SupraConfig.RegisterGroup('theme', 'General options related to the theme and colorscheme')

SupraConfig.RegisterMany([
    # --- GENERAL ---
    {
        id: 'theme',
        type: 'string',
        default: 'onedark',
        lore: 'Sets the main color scheme',
        handler: (v) => {
			# a theme can be have the format 'theme[-<light|dark>]'
			var name = matchstr(v, '^[^-]*')
			var typemode = matchstr(v, '-\zs.*') == 'light' ? 'light' : 'dark'
			Options.ChangeSupravimTheme(name, typemode)
		}
    },
    {
        id: 'swap',
        type: 'bool',
        default: false,
        lore: 'Enable the creation of swap files (.swp)',
        handler: (v) => {
			# exec ($'{v}' == 'true' ? 'set swapfile' : 'set noswapfile')
			&swapfile = v
	}
    },
    {
        id: 'wrap',
        type: 'bool',
        default: true,
        lore: 'Enable automatic line wrapping',
        handler: (v) => {
			# exec ($'{v}' == 'true' ? 'set wrap' : 'set nowrap')
			&wrap = v 
		}
    },
    {
        id: 'autosave',
        type: 'bool',
        default: false,
        lore: 'Automatically save files on changes',
        handler: (v) => {
			# g:sp_autosave = ($'{v}' == 'true')
			g:sp_autosave = v
		}
    },
    {
        id: 'nobackground',
        type: 'bool',
        default: false,
        lore: 'Remove background color to use terminal transparency',
        handler: (v) => {
			Options.ChangeNoBackgroundOption(v)
		}
    },

    # --- AUTO PAIRS ---
    {
        id: 'autopairs/enabled',
        type: 'bool',
        default: true,
        lore: 'Enable automatic closing of brackets and quotes',
        handler: (v) => {
			g:autopairs_enabled = v
		}
    },
    {
        id: 'autopairs/flymode',
        type: 'bool',
        default: false,
        lore: 'Enable Fly Mode for Auto Pairs (experimental)',
        handler: (v) => {
			g:AutoPairsFlyMode = v
		}
    },

    # --- FUZZBOX ---
    {
        id: 'fuzzbox/enable_icons',
        type: 'bool',
        default: true,
        lore: 'Show file icons in Fuzzbox search results',
        handler: (v) => {
			g:fuzzbox_enable_icons = v 
		}
    },
    {
        id: 'fuzzbox/dropdown',
        type: 'bool',
        default: false,
        lore: 'Display the search prompt at the top instead of the bottom',
        handler: (v) => {
			g:fuzzbox_dropdown = v
		}
    },
    {
        id: 'fuzzbox/counter',
        type: 'bool',
        default: true,
        lore: 'Show the number of matches in Fuzzbox',
        handler: (v) => {
			g:fuzzbox_counter = v
		}
    },
    {
        id: 'fuzzbox/scrollbar',
        type: 'bool',
        default: false,
        lore: 'Show a scrollbar when results exceed window size',
        handler: (v) => {
			g:fuzzbox_scrollbar = v
		}
    },
    {
        id: 'fuzzbox/prompt_prefix',
        type: 'string',
        default: "➜ ",
        lore: 'Prefix character for the Fuzzbox input prompt',
        handler: (v) => {
			g:fuzzbox_prompt_prefix = v
		}
    },
    {
        id: 'fuzzbox/selection_prefix',
        type: 'string',
        default: "➜ ",
        lore: 'Prefix character for the selected item in Fuzzbox results',
		handler: (v) => {
			g:fuzzbox_selection_sign = v
		}
    },
    {
        id: 'fuzzbox/wrap_menu',
        type: 'bool',
        default: false,
        lore: 'Wrap long lines in the Fuzzbox menu',
        handler: (v) => {
			g:fuzzbox_wrap_menu = v
		}
    },
    {
        id: 'fuzzbox/wrap_preview',
        type: 'bool',
        default: true,
        lore: 'Wrap long lines in the Fuzzbox preview window',
        handler: (v) => {
			g:fuzzbox_wrap_preview = v 
		}
    },

    # --- ROUNDVISUAL (AUTO CLOSING) ---
    {
        id: 'roundvisual/quotes',
        type: 'bool',
        default: true,
        lore: 'Auto-close quotes " and' .. " \' and `'",
        handler: (v) => {
			g:roundvisual_quotes = v 
		}
    },
    {
        id: 'roundvisual/brackets',
        type: 'bool',
        default: true,
        lore: 'Auto-close parenthesis () and brackets []',
        handler: (v) => {
			g:roundvisual_brackets = v 
		}
    },
    {
        id: 'roundvisual/braces',
        type: 'bool',
        default: false,
        lore: 'Auto-close braces {}',
        handler: (v) => {
			g:roundvisual_braces = v
		}
    },
    {
        id: 'roundvisual/angle',
        type: 'bool',
        default: false,
        lore: 'Auto-close angle brackets <>',
        handler: (v) => {
			g:roundvisual_angle = v
		}
    },

    # --- UI ELEMENTS ---
    {
        id: 'symbol_signs',
        type: 'string',
        default: "┃",
        lore: 'Vertical symbol used for sidebar signs (e.g. Git markers)',
        handler: (v) => {

			var clean = substitute($'{v}', '\s+', '', 'g')
            g:sp_symbol_signs = (empty(clean) || strcharlen(clean) > 2) ? '✖' : clean
        }
    },
    {
        id: 'mapleader',
        type: 'string',
        default: "\<space>",
        lore: 'The leader key used for Supravim shortcuts',
        handler: (v) => {
			g:mapleader = v
			g:maplocalleader = v
		}
    },

    # --- THEME STYLES ---
    {
        id: 'theme/style/function',
        type: 'string',
        default: 'italic,bold,underline',
        lore: 'Style for functions none, bold, italic, underline', 
        handler: (v) => {
			Options.ChangeColorStyleStatic('Function', v)
		}
    },
    {
        id: 'theme/style/type',
        type: 'string',
        default: 'none',
        lore: 'Style for types none, bold, italic, underline',
        handler: (v) => {
			Options.ChangeColorStyleStatic('Type', v)
		}
    },
    {
        id: 'theme/style/variable',
        type: 'string',
        default: 'none',
        lore: 'Style for variables none, bold, italic, underline',
        handler: (v) => {
			Options.ChangeColorStyleStatic('Variable', v)
		}
    },
    {
        id: 'theme/style/keyword',
        type: 'string',
        default: 'none',
        lore: 'Style for keywords none, bold, italic, underline',
        handler: (v) => {
			Options.ChangeColorStyleStatic('Keyword', v)
		}
    },
    {
        id: 'theme/style/comment',
        type: 'string',
        default: 'none',
        lore: 'Style for comments none, bold, italic, underline',
        handler: (v) => {
			Options.ChangeColorStyleStatic('Comment', v)
		}
    },
	{
		id: 'theme/style/string',
		type: 'string',
		default: 'none',
		lore: 'Style for strings none, bold, italic, underline',
		handler: (v) => {
			Options.ChangeColorStyleStatic('String', v)
		}
	},

])

#------------- Cursor Parts ------------"
&t_SI = "\<Esc>[5 q"
&t_SR = "\<Esc>[3 q"
&t_EI = "\<Esc>[2 q"
autocmd VimLeave * !echo -ne "\e[0 q"

#-------------- Save Undo  ------------"
set undodir=~/.cache/vim/undo
set undofile
set viminfo+=!

# -------------  DBG integration  --------------"
packadd! termdebug
g:termdebug_wide = 1

#--------------- Shorcut --------------"
nnoremap <c-n>			<esc>:tabnew 
nnoremap <C-Right>		<scriptcmd>tabnext<CR>
nnoremap <C-Left>		<scriptcmd>tabprevious<CR>
nnoremap <C-Up>		<scriptCmd>call Ctags()<CR>
nnoremap <C-Down>	<scriptcmd>pop<cr>
inoremap <C-Up>		<scriptcmd>call Ctags()<CR>
inoremap <C-Down>	<scriptcmd>pop<cr>
inoremap <c-q>		<cmd>q!<CR>
noremap <c-q>		<cmd>q!<CR>
inoremap <c-s>		<cmd>w!<CR><esc>
noremap <c-s>		<cmd>w!<CR><esc>
nnoremap <C-F5> 	<cmd>Termdebug -n <CR>
nnoremap <F2> 		<ScriptCmd>call Utils.RenameSymbol()<CR>
inoremap <F2> 		<ScriptCmd>call Utils.RenameSymbol()<CR>
nnoremap <leader>v		:vs<cr>
nnoremap <leader>s		:split<cr>
nnoremap <S-Right>	<C-w><Right>
nnoremap <S-Left>	<C-w><Left>
nnoremap <S-Up>		<C-w><Up>
nnoremap <S-Down>	<C-w><Down>
nnoremap <S-Tab>	<<
nnoremap <Tab>		>>
vnoremap <S-Tab>	<<<esc>gv
vnoremap <Tab>		>><esc>gv
inoremap <c-a>		<esc>gg<s-v>G
nnoremap <silent><c-@>		<cmd>LspHover<cr>
silent! unmap <c-t>
nnoremap <C-t>		<scriptcmd>term ++rows=15<CR>
# Terminal shortcuts
map <leader>tt	<scriptcmd>call SupraTerm()<cr>
map <leader>tf	<scriptcmd>call SupraTerm()<cr>
map <leader>th	:horizontal term<CR>
map <leader>tv	:vertical term<CR>
# Lsp shortcuts 
nnoremap <silent> gd		<cmd>LspDefinition<cr>
nnoremap <silent> gr		<cmd>LspReferences<cr>
nnoremap <silent> gi		<cmd>LspImplementation<cr>
nnoremap <silent> gD		<cmd>LspTypeDefinition<cr>
nnoremap <silent> gt		<cmd>LspTypeDefinition<cr>
nnoremap <silent> g0		<cmd>LspDocumentSymbol<cr>
nnoremap <silent> gW		<cmd>LspWorkspaceSymbol<cr>
nnoremap <silent> ga		<cmd>LspCodeAction<cr>
nnoremap <silent> K			<cmd>LspHover<cr>

# Other
inoremap <c-z>		<esc>ui
inoremap <c-y>		<esc><c-r>i

# Move lines
vnoremap <silent> <A-Down>	:<scriptcmd>Utils.MoveLineDown("v")<CR><cr>
vnoremap <silent> <A-Up>	:<scriptcmd>Utils.MoveLineUp("v")<CR><cr>
nnoremap <silent> <A-Down>	<scriptcmd>Utils.MoveLineDown("n")<CR>
nnoremap <silent> <A-Up>	<scriptcmd>Utils.MoveLineUp("n")<CR>

# Fuzzy Finder
nnoremap <silent> <leader>g			<scriptcmd>FuzzyGrep<CR>
nnoremap <silent> <leader>m			<scriptcmd>FuzzyMarks<CR>
nnoremap <silent> <leader>f			<scriptcmd>FuzzyFiles<CR>
nnoremap <silent> <leader>d			<scriptcmd>FuzzyLspDiag<CR>
nnoremap <silent> <leader>b			<scriptcmd>FuzzyBuffers<CR>
nnoremap <silent> <leader>h			<scriptcmd>FuzzyHighlights<CR>
nnoremap <silent> <leader>c			<scriptcmd>FuzzyClip<cr>
nnoremap <silent> <leader>p			<scriptcmd>FuzzyPrevious<cr>
nnoremap <silent> <leader><f5>		<scriptcmd>FuzzyMake<cr>
# nnoremap <silent> <leader>t			<scriptcmd>FuzzySupraTags<cr>
nnoremap <silent> <leader><leader>	<scriptcmd>FuzzyBuffers<CR>

# SupraVim Settings
inoremap <F12>	<scriptcmd>call SettingsSupravim()<cr>
noremap <F12>	<scriptcmd>call SettingsSupravim()<cr>
# Mouse
nnoremap <RightMouse> <esc><LeftMouse><esc><RightMouse>
inoremap <RightMouse> <esc><LeftMouse><esc><RightMouse>
nnoremap <c-leftmouse>	<leftmouse><scriptcmd>call Ctags()<cr>

#---------------      Command        ---------------"
cmap <c-q>	q!<cr>
cmap <c-s>	w!<cr>
cmap <c-v>	<c-r>0<del>

#---------------      Terminal        ---------------"
tnoremap <C-q> exit<CR>
tnoremap <F3> clear -x ; norminette<CR>
tnoremap <F5> supramake run<CR>
tnoremap <F6> supramake run2<CR>
tnoremap <F7> supramake run3<CR>
tnoremap <esc>	<c-\><c-n>
# Window navigation
tnoremap <S-Right>		<C-W>N<C-w><Right>
tnoremap <S-Left>		<C-W>N<C-w><Left>
tnoremap <S-Up>			<C-W>N<C-w><Up>
tnoremap <S-Down>		<C-W>N<C-w><Down>

#--------------- 	Settings      ---------------"
g:lsp_log_verbose = 0
set mouse=a
set cursorline
set shortmess+=I
set nu
set incsearch
if executable('suprapack') == 0
	autocmd VimEnter * call Notify.Notification(["Suprapack is not found", "Please source the $HOME/.suprapack_profile", "Or add ~/.local/bin to your path"], {type: 'warning'})
	$PATH = $PATH .. ':' .. expand('$HOME') .. '/.local/bin:'
endif
set noautochdir
silent! exec 'cd ' .. expand('%:p:h')
silent! var project_root = system('suprabear -p')
var suprabear_return = v:shell_error
silent! exec 'chdir ' .. project_root
set autoindent
set backspace=indent,eol,start
set breakindent
set encoding=utf-8
set notimeout
set pumheight=50
set shiftround
set shiftwidth=4
set showmode
set smartindent
set splitbelow
if exists('+smoothscroll')
	set smoothscroll
endif
syntax sync minlines=100
hi! link Pmenu Normal
if exists('+pumborder')
	set pumborder=round
	set pumheight=10
endif
set display=lastline
set splitright
set tabstop=4
set timeoutlen=100
set title
set ttimeout
set wildmenu
au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif
set fillchars=vert:│
auto BufEnter,VimEnter *.tpp set filetype=cpp

# ---------------      LSP            ---------------"
set updatetime=500
autocmd CursorHold * call Utils.AutoLspHold()
#--------------- Lsp Interface ---------------"
def g:AddLspServer(name: string, cmd: list<string>, filetypes: list<string>)
	silent! lsp#register_server({
		name: name,
		cmd: (_) => cmd,
		allowlist: filetypes,
	})
	silent! lsp#activate()
enddef

def g:LspServerActivate()
	silent! lsp#activate()
enddef

au User lsp_setup doautocmd User LspIsReady

# --------------- Auto Pairs ---------------"
g:AutoPairsFlyMode = 0

#--------------- Supravim Options ---------------"
def g:SettingsSupravim()
	Options.OpenWindow()
enddef

# ---------------  SupraVim Server  ---------------"
import autoload 'supravim/SupravimServer.vim' as SupraVimServer

SupraVimServer.RunServer(project_root)

# ---------------  GDB  ---------------"

imap <C-F5>		<ScriptCmd>Gdbs<CR>
map <C-F5>		<ScriptCmd>Gdbs<CR>

command -nargs=0 -bar Gdbs :call Gdbf()

def Gdbf()
	if &filetype != 'c' && &filetype != 'cpp' && &filetype != 'vala' && &filetype != 'hpp'
		echo "Tu veux debugger quoi là ?"
	else
		set splitbelow nosplitbelow
		set splitright nosplitright
		SupraTreeClose
		if !filereadable("Makefile")
			exec ":Termdebug -n ./a.out"
		else
			exec ":Termdebug -n"
		endif
	endif
	set splitbelow
	set splitright
enddef
# -------------- Ctags ----------------"

command -nargs=0 -bar Ctags :call Ctags()

set tags=$HOME/.cache/tags

def Ctags()
	try
	LspDefinition
	catch
		system("supratags --output=" .. expand("$HOME") .. "/.cache/tags")
		execute "tag " .. expand("<cword>")
	endtry
enddef

# ----------------- AIR LINE ------------------
g:airline#extensions#whitespace#enabled = 0
g:airline_section_z = airline#section#create(['Line:%l', 'hunks', ' Col:%c'])
g:airline_section_b = airline#section#create(['SupraVim'])
g:airline_left_sep = '◤'
g:airline_left_alt_sep = '╱'
g:airline_right_sep = '◥'
g:airline_right_alt_sep = '╲'
g:airline#extensions#tabline#enabled = 1
g:airline#extensions#tabline#show_buffers = 0
g:airline#extensions#tabline#tabs_label = 'Tabs'
g:airline#extensions#tabline#buffer_nr_show = 0
g:airline#extensions#tabline#tab_nr_type = 1
g:airline#extensions#tabline#left_sep = '◤'
g:airline#extensions#tabline#left_alt_sep = '╱'
g:airline#extensions#tabline#right_sep = '◥'
g:airline#extensions#tabline#right_alt_sep = '╱'

# ----------------- FuzzBox ------------------
g:fuzzbox_enable_mappings = 0
g:fuzzbox_files_exclude_file = ['*.o', '*.png', '*.jpg', '*.gif', 'compile_commands.json', '*.beam', '*.so', '*.exe', '*.dll', '*.dump', '*.core', '*.swn', '*.swp', '*.a', '*.out']
g:fuzzbox_files_exclude_dir = ['build', '*.cache', '.git', '.hg', '.svn', '.rebar', '.eunit']
g:fuzzbox_devicons_color_func = 'SupraIcons#Palette#Apply'

# ----------------- POPUP ------------------
autocmd VimEnter * CreatePopit()

def PopUpUpdate(channel: channel, msg: string)
	Notify.Notification(["Update", msg, "run: `suprapack update`", "Click here to update"], {
		left_click: (_) => {
			!suprapack update
		},
		icon: '󰚰'
	})
enddef

def CreatePopit()
	job_start(["suprapack", "have_update", "supravim"], {
		out_cb: PopUpUpdate,
	})
enddef

# ---------------  Popup Menu  ---------------"
set mousemodel=popup

# ---------------  SetARGS  ---------------"
command -nargs=* -complete=file SetArgv :call SetArgv(<f-args>)
command -nargs=* -complete=file SetArgs :call SetArgv(<f-args>)

def SetArgv(...args: list<string>)
	var str = join(args, ' ')
	$ARGS = str
enddef

# ---------------  Welcome ---------------"
autocmd BufEnter * {
	if expand('%:e') == 'water'
		&titlestring = '[Supravim] Editing on SupraWater'
	else
		&titlestring = '[Supravim]  ' .. expand('%:t')
	endif
}

g:loaded_netrw = 1
g:loaded_netrwPlugin = 1

# Force directory creation on save
autocmd BufWritePre * {
if filereadable(expand('%')) == 0
	mkdir(expand('%:p:h'), 'p')
endif
}
nnoremap <c-g> <cmd>SupraTreeToggle<cr> 

#--------------- SupraVim Color ----------"
def FixSupraVimColor()
	hi! link SignColumn Normal
	hi! link EndOfBuffer Normal
enddef

autocmd Colorscheme * call FixSupraVimColor()
FixSupraVimColor()

autocmd VimEnter * call WelcomeModule.PrintChangeLog()

# for the bug with line with space
hi mkdLineBreak cterm=NONE

#--------------- Games ---------------"
autocmd VimEnter __SNAKE__ {
	:Snake
}

autocmd VimEnter __PACMAN__ {
	:Pacman
}

autocmd VimEnter * call WelcomeModule.Welcome()
